buildscript {
    ext.kotlin_version = '1.1.3'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

def GROUP_ID = 'com.andreapivetta.kolor'
def ARTIFACT_ID = 'kolor'
def VERSION = '0.0.1'

group GROUP_ID
version VERSION

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    testCompile 'com.winterbe:expekt:0.4.0'
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile 'org.junit.platform:junit-platform-runner:1.0.0-M4'
    testCompile('org.jetbrains.spek:spek-api:1.1.2') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime('org.jetbrains.spek:spek-junit-platform-engine:1.1.2') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION
        }
    }
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_KEY
    publications = ['MyPublication']
    pkg {
        repo = ARTIFACT_ID
        name = ARTIFACT_ID
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ziggy42/kolor.git'
        version {
            name = VERSION
            released = new Date()
        }
    }
}